 r←{fullPath}ListCache url;path;list;list_;domains;domains_;packages
⍝ Lists the contents of the Tatin package cache.
⍝ If url is empty all packages for all urls are listed.
⍝ The result is a vector with as many items as there are domains represented in the cache with at least one package.
⍝ Each item is a two-element vector:
⍝  * The first item holds the url of the domain
⍝  * The second item holds a vector of char vectors with the package names
⍝ In case the cache is empty an empty vector is returned.
⍝ If a url is specified but does not exist in the cache, ditto.
⍝ `fullPath` defaults to 0, meaning that just domain names and packages names are returned.
⍝ Specify a 1 as ⍺ and you get full path names instead.
⍝ ---
⍝ You might want to check the result in a readable format; for that execute:
⍝ ⎕←⍪{⍪('*** ',1⊃⍵)(⍪2⊃⍵)}¨ListCache ⍬
 r←''
 fullPath←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'fullPath'
 url←ReplaceRegistryAlias url
 url←RemoveHttpProtocol url
 path←GetPathToPackageCache
 :If 0=≢url
     :If 0<≢domains←{⍵/⍨(⊂'temp')≢¨2⊃¨⎕NPARTS¨⍵}F.ListDirs path,'/*'
         domains_←{{⊃,/1↓⎕NPARTS ⍵}⍣(~fullPath)⊣⍵}¨domains
         domains_←{':'@(⍸'∆'=⍵)⊣⍵}¨domains_  ⍝ Restore port number
         packages←F.ListDirs¨domains
         packages←{GetPackageIDFromFilename¨¨⍵}⍣(~fullPath)⊣packages
         r←(⊂¨domains_),¨⊂¨packages
         r←(0<≢¨2⊃¨r)/r                      ⍝ Drop domains without packages
         :If 0=≢r
             r←''
         :EndIf
     :EndIf
 :Else
     :If F.IsDir path,'/',url
         packages←F.ListDirs path,'/',url
         domains_←path{⍺,'/',⍵}⍣fullPath⊣url
         :If 0<≢packages
             r←,⊂(domains_)({{⊃,/1↓⎕NPARTS ⍵}⍣(~fullPath)⊣⍵}¨packages)
         :EndIf
     :EndIf
 :EndIf
 ⍝Done
