 (alias url)←SplitAtAlias id;pkgID;regAlias;buff
⍝ Splits "id" at the package (not Registry!) alias, if any'
⍝ ('M@' '[tatin]MarkAPL') ← SplitAtAlias 'M@[tatin]MarkAPL'
⍝ ('' '[tatin]MarkAPL') ← SplitAtAlias '[tatin]MarkAPL'
 alias←''
 :If '@'∊id
     (alias pkgID)←id{(⍵↑⍺)(⍵↓⍺)}id⍳'@'
     :If ∧/'[]'∊alias
         (regAlias alias)←{⍵{(⍵↑⍺)(⍵↓⍺)}⍵⍳']'}alias
         'Invalid package alias'Assert 1<≢alias
         'Invalid package alias'Assert 0=(⎕NS'').⎕NC ¯1↓alias
         url←regAlias,pkgID
     :Else
         :If ∨/'/\'∊alias
             (alias buff)←{⍵{(⍵↑⍺)(⍵↓⍺)}1+-⌊/(⌽⍵)⍳'\/'}alias
             url←buff,pkgID
         :Else
             url←pkgID
         :EndIf
     :EndIf
 :Else
     url←id
 :EndIf
⍝Done
