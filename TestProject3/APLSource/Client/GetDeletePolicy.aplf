 r←{check}GetDeletePolicy url;request;res;client;buff;row;url_
⍝ Takes a URI of a server (like https://tatin.dev) and returns the delete policy operated by the given server.
⍝ Note that for a file (rather than a URL) "Any" is returned, otherwise we could not delete a folder from a non-managed Registry.
⍝ If there is a global ∆DeletePolicies' around (which will be created by this function after the first call
⍝ for better performance) then this will be used to determine the delete policy.
 check←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'check'
 :If check
     ⎕EX'∆DeletePolicies'
 :EndIf
 url_←ReplaceRegistryAlias url
 'No URI provided'Assert 0<≢url_
 :If Reg.IsHTTP url_
     :If 0=⎕NC'∆DeletePolicies'
         ∆DeletePolicies←0 2⍴''
     :EndIf
     row←∆DeletePolicies[;1]⍳⊂url_
     :If row>≢∆DeletePolicies
         request←R.NewRequest 0
         request.RequestTarget←'/v1/delete-policy'
         request.Headers,←⊂'Accept' 'text/html'
         res←SendAndReceive url_ request
         :If 200≠res.StatusCode
             'Server not found'Assert 404≠res.StatusCode
             'Bad request'Assert 400≠res.StatusCode
             :If 0<≢res.Content
                 ('Server: ',res.Content)Assert 0
             :Else
                 ('Failed with status code ',⍕res.StatusCode)Assert 0
             :EndIf
         :EndIf
         r←res.Content
         ∆DeletePolicies⍪←url_ r
     :Else
         r←2⊃∆DeletePolicies[row;]
     :EndIf
 :Else
     r←'Any'
 :EndIf
⍝Done
