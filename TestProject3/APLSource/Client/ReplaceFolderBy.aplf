 success←old ReplaceFolderBy new;temp;flag;qdmx;msg;rc
⍝ Performs the following actions:
⍝ * Establish a temp name dynamically
⍝ * Rename "old" to "temp"
⍝ * Re-create the folder "old"
⍝ * Copy the contents of "new" into "old"
⍝ * Delete "new"
⍝ * Delete "temp"
 temp←'-'{~⎕NEXISTS n←⍺{(1⊃⍵),⍺,(2⊃⍵)}⎕NPARTS ⍵:n ⋄ (⍺,⍺)∇ ⍵}{⍵↓⍨-(¯1↑⍵)∊'/\'}old
 flag←0
 :Repeat
     :Trap 19 22
         temp(⎕NMOVE F.ExecNfunction)old
         flag←1
     :Else
         qdmx←⎕DMX
         :If ~YesOrNo'Attempt to replace ',old,' resulted in a ',qdmx.EM,' error; try again?'
             'Cancelled by user after error'⎕SIGNAL 98
         :EndIf
     :EndTrap
 :Until flag
 F.MkDir old
 :Trap 0
     old(⎕NCOPY⍠('Wildcard' 1)F.ExecNfunction)new,'/*'
     {}F.RmDirByForce new
     success←1
 :Else                      ⍝ Roll back
     {}F.RmDirByForce old
     F.MkDir old
     old(⎕NMOVE⍠('Wildcard' 1)F.ExecNfunction)temp,'/*'
     success←0
 :EndTrap
 (rc msg)←F.RmDirByForce temp
 :If rc≠0
     ⎕←'*** Warning: removing ',temp,' failed but the re-installed worked!'   ⍝TODO⍝ This has failed a number of times, and we don't know yet why
 :EndIf
