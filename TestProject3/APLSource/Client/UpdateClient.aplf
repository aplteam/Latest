 (version ref target)←{tag}UpdateClient y;force;GH;request;client;data;data2;redirect;tempFile;tno;folder;wspath;msg;flag;rc;⎕TRAP;filename;currentVersion
⍝ Updates the client to (by default) the latest version, and returns the version number of the new version.\\
⍝ A Boolean right argument (`force`) is mandatory, but one might specify also a folder Tatin is installed in.
⍝ The user command will do this automatically with the folder the user command was loaded from.\\
⍝ Asks for confirmation from the user unless `force` is 1.\\
⍝ If you want to update to a specific version you may specify the desired version in ⍺.
⍝ The format must be something like "v1.0.2".
 (force target)←2↑(⊆y),⊂''
 'Right argument must be a Boolean'Assert(⊂force)∊0 1
 'Updating Tatin requires at least 20 MB WSSIZE'Assert 20<⌊⎕WA÷1024×1024
 version←ref←''
 :If flag←0=⎕NC'tag'
     GH←⎕NEW ##.GitHubAPIv3(,⊂'aplteam')
     filename←GetMyUCMDsFolder'Tatin/GitHub-personal-token.txt'
     :If F.IsFile filename
         GH.personal_access_token←∊⊃F.NGET filename 1
     :EndIf
     currentVersion←({⍵↑⍨¯1+⌊/⍵⍳'+-'}2⊃Reg.Version)
     tag←GH.CheckForUpdate'Tatin'currentVersion
 :Else
     'Invalid version number'Assert'v'=1⍴tag
 :EndIf
 :If 0=≢tag
     ⎕←'Tatin is up-to-date: ',⊃{⍺,' from ',⍵}/1↓Reg.Version
 :Else
     msg←'You version of Tatin is ',2⊃Reg.Version
     msg,←(⎕UCS 13),'Latest published version is ',1↓tag
     msg,←(⎕UCS 13),'Would you like to download and install ',(1↓tag),' ?'
     :If ~flag ⍝ Then the user has requested a particular version, and we assume she knows what she's doing
     :OrIf msg{⍵:⍵ ⋄ YesOrNo ⍺}force
         request←R.NewRequest 0
         request.RequestTarget←'/aplteam/Tatin/releases/download/',tag,'/Tatin-Client-',(1↓tag),'.zip'
         client←R.NewClient 0
         client.Host←'github.com'
         request.Headers,←⊂'User-Agent' 'aplteam'
         data←client R.SendAndReceive request
         'Invalid HTTP status code'Assert 302≡data.StatusCode
         redirect←2⊃'location'{⍵⊃⍨(⊃¨⍵)⍳⊂⍺}data.Headers
         redirect←'https://'{l←≢⍺ ⋄ ⍵↓⍨l×⍺≡l↑⍵}redirect
         client←R.NewClient 0
         (client.Host request.RequestTarget)←{l←+/1>+\'/'=⍵ ⋄ (l↑⍵)(l↓⍵)}redirect
         data2←client R.SendAndReceive request
         folder←F.GetTempSubDir'Tatin-Upgrade'
         tempFile←folder,'/',tag,'.zip'
         tno←tempFile ⎕NCREATE 0
         data2.Content ⎕NAPPEND tno 80
         ⎕NUNTIE tno
         data2.Content←''
         tempFile ZipArchive.UnzipTo folder
         ⎕NDELETE tempFile
         :If 0=≢target
             target←GetMyUCMDsFolder'Tatin'  ⍝ Establish default
         :EndIf
         (⊃⎕NPARTS{⍵↓⍨-'/\'∊⍨¯1↑⍵}target)⎕NCOPY⍠('IfExists' 'Replace')⊣folder,'/Tatin'
         version←{⍵↓⍨'v'=1⍴⍵}tag
         ref←⎕SE.⎕NS''
     :EndIf
 :EndIf
⍝Done
