 {r}←{force}UpdatePackages dummy;path2Packages;targetFolder;list;F;parms;TC;res
⍝ 1. Re-installs all packages in the ./packages folder
⍝ 2. Copies the required scripts from the folder packages/ to APLSource/ by calling CopyScriptsFromPackagesIn
⍝ 3. The assets required by MarkAPL are copied into ./Assets/MarkAPL\\
⍝ Returns a 1 if any of the scripts was actually updated, and 0 otherwise.
 r←0
 force←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'force'
 :If force
 :OrIf ##.Client.YesOrNo'   Sure you want to re-install and possibly update all Tatin packages?'
     :If 0=##.Client.⎕NC'Reg'
         Initialize_Client
     :EndIf
     F←##.FilesAndDirs ⋄ TC←##.Client
     path2Packages←##.CiderConfig.HOME,'/packages'
     targetFolder←##.CiderConfig.HOME,'/APLSource'
     ⎕NDELETE F.ListFiles targetFolder,'/*.aplc'
     parms←TC.CreateReInstallParms
     parms.update←1
     ⎕←'Now updating packages in ',path2Packages,'/ ...'
     res←parms TC.ReInstallDependencies path2Packages               ⍝ <==
     ⎕←{0=≢⍵:'No packages updated' ⋄ ⍪(⊂'*** Packages updated:'),⍵}res
     r←targetFolder CopyScriptsFromPackagesIn path2Packages
     {}{⎕←'*** Done'}⍣(~force)⊣⍬
 :EndIf
⍝Done
