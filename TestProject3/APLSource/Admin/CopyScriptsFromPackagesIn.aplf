 {r}←{targetFolder}CopyScriptsFromPackagesIn path2Packages;packages;i;package;packageID;packageName;fullName;F
⍝ Copy scripts installed in `path2Packages` over to `targetFolder` which defaults to APLSource/
⍝ Note that packages that are not just scripts are NOT handled. CommTools and FilesAndDirs are examples.
 r←0
 F←##.FilesAndDirs
 targetFolder←{0<⎕NC ⍵:⍎⍵ ⋄ './APLSource/'}'targetFolder'
 packages←{⍵.packageID}##.Registry.APLfromJSON⊃⎕NGET path2Packages,'/apl-buildlist.json'
 :For i package :InEach (⍳≢packages)packages
     packageID←⊃,/1↓⎕NPARTS package
     packageName←{{⍵↑⍨¯1+⍵⍳'-'}⍵↓⍨⍵⍳'-'}packageID
     fullName←⊃F.Dir targetFolder,'/',packageName,'.*'
     :If 0=≢fullName
         :If F.IsDir path2Packages,'/',packageID,'/APLSource'
             r∨←(targetFolder,'/')(⎕NCOPY⍠('IfExists' 'ReplaceIfNewer')('Wildcard' 1)F.ExecNfunction)path2Packages,'/',packageID,'/APLSource/*'
         :Else
             r∨←(targetFolder,'/')(⎕NCOPY⍠('IfExists' 'ReplaceIfNewer')('Wildcard' 1)F.ExecNfunction)path2Packages,'/',packageID,'/',packageName,'*'
         :EndIf
     :Else ⍝ Simple files
         fullName←⊃F.Dir path2Packages,'/',package,'/',packageName,'.*'
         r∨←(targetFolder,'/')(⎕NCOPY⍠('IfExists' 'ReplaceIfNewer')F.ExecNfunction)fullName
     :EndIf
 :EndFor
⍝Done
