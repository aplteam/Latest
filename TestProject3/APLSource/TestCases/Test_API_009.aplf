 r←Test_API_009(stopFlag batchFlag);⎕TRAP;rc
 ;dir;dir2;oldDependencies;oldBuildlist;msg;list;oldPkgList;newPkgList;newBuildlist;newDependencies
⍝ Exercise ⎕SE.Tatin.UnInstallPackages: un-install by alias a package that is also installed by just its package ID
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 dir←∆GetTempDir''
 dir ⎕NCOPY'TestData/UnInstall6/'
 dir2←dir,'/UnInstall6'
 oldDependencies←1⊃F.NGET(dir2,'/',TC.Reg.DependenciesFilename)1
 oldBuildlist←Reg.GetJsonFromFile dir2,'/',TC.Reg.BuildListFilename
 oldPkgList←F.ListDirs dir2,'/'

 (list msg)←⎕SE.Tatin.UnInstallPackages'P@'dir2
 →T.GoToTidyUp 0≠≢list
 →T.GoToTidyUp 0≠≢msg

 newDependencies←1⊃F.NGET(dir2,'/',TC.Reg.DependenciesFilename)1
 newBuildlist←Reg.GetJsonFromFile dir2,'/',TC.Reg.BuildListFilename
 newPkgList←F.ListDirs dir2,'/'
 →T.GoToTidyUp 1≠-/≢¨oldDependencies newDependencies
 →T.GoToTidyUp 0≠-/≢¨oldPkgList newPkgList

 (list msg)←⎕SE.Tatin.UnInstallPackages'aplteam-APLProcess-0.4.1'dir2
 →T.GoToTidyUp~list ∆LazyMatch'aplteam-APLProcess-0.4.1' 'aplteam-APLTreeUtils2-1.1.1' 'aplteam-OS-3.0.1'
 →T.GoToTidyUp 0≠≢msg

 newDependencies←1⊃F.NGET(dir2,'/',TC.Reg.DependenciesFilename)1
 newBuildlist←Reg.GetJsonFromFile dir2,'/',TC.Reg.BuildListFilename
 newPkgList←F.ListDirs dir2,'/'
 →T.GoToTidyUp 2≠-/≢¨oldDependencies newDependencies  ⍝ 2 because "OS" is a dependency
 →T.GoToTidyUp 1≠≢newPkgList

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce dir
 msg Assert rc=0
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
