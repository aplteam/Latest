 r←Test_UserCommands_071(stopFlag batchFlag);⎕TRAP;dir;old;res;rc;msg;new;stop
⍝ Exercise ]Tatin.ReInstallDependencies
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 dir←∆GetTempDir''
 dir ⎕NCOPY⍠('Wildcard' 1)⊣'TestData/UnInstall/*'
 old←(1+≢dir)↓¨##.FilesAndDirs.ListDirs dir,'/'
 ##.FilesAndDirs.RmDir dir,'/aplteam-OS-3.0.1'
 stop←0
 (rc msg res)←stop ∆UCMD'ReInstallDependencies "',dir,'" -force'
 →T.GoToTidyUp 0≠rc
 →T.GoToTidyUp 0≠≢msg

 new←(1+≢dir)↓¨##.FilesAndDirs.ListDirs dir,'/'
 →T.GoToTidyUp~∧/(TC.Reg.RemoveMinorAndPatch¨old)∊TC.Reg.RemoveMinorAndPatch¨new

 r←T._OK

∆TidyUp:
 #.⎕EX TC.GetTatinRootSpace'#'
 (rc msg)←F.RmDirByForce dir
 msg Assert rc=0
⍝Done
