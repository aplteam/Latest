 r←Test_PkgCfg_004(stopFlag batchFlag);⎕TRAP;res;qdmx
 ;dir;src;zip;cfg;refs
⍝ Create a package config file with two asset files that are addressed incorrectly
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 #.⎕SHADOW'TEMP' ⋄ 'TEMP'#.⎕NS'' ⍝ Used to load stuff into
 dir←∆GetTempDir ⍬
 (src zip)←(⊂dir,'/'),¨'src' 'reg'
 F.MkDir src zip

 F.MkDir(739⌶0),'/',⊃⎕SI
 (⊂'first')F.NPUT(739⌶0),'/',(⊃⎕SI),'/first.dll'

 cfg←TC.InitPackageConfig ⍬
 cfg.(group name version)←'Example' 'Foo' '0.1.0'
 cfg.source←'Foo.apln'
 cfg.api←'Foo'
 cfg.assets←dir,'/first.dll'
 cfg.os_win←1
 :Trap 998
     TC.WritePackageConfigFile src cfg
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp'"assets" must be addressed by a relativ path'≢qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 Assert 0=⊃F.RmDir dir
 F.DeleteFile(739⌶0),'/',(⊃⎕SI),'/first.dll'
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
