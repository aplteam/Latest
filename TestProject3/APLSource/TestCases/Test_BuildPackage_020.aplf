 r←Test_BuildPackage_020(stopFlag batchFlag);⎕TRAP;rc;msg;parms
 ;dir;src;dist;body;cfg;zipfile;ns;qdmx
⍝ Build a single file (function) with an incompatible "source" setting.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 F.MkDir src dist

 body←'r←Hello' 'r←''World'''
 (⊂body)F.NPUT src,'/Hello.aplf'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Hello'
 cfg.version←'0.1.0'
 cfg.source←'Firlefanz.aplf'
 cfg.os_win←1
 TC.WritePackageConfigFile src cfg

 parms←TC.CreateBuildParms src
 parms.targetPath←dist
 :Trap 998
     zipfile←TC.BuildPackage parms
     →T.GoToTidyUp 1
 :Else
     qdmx←⎕DMX
     →T.GoToTidyUp~∨/'"Firlefanz.aplf" is neither file nor folder'⍷qdmx.EM
 :EndTrap

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce dir
 msg Assert rc=0
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
