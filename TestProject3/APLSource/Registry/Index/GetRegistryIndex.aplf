 r←{nested}GetRegistryIndex path;filename;list
⍝ In a managed Tatin Registry there must be a file "tatin_index.txt".
⍝ In a local un-managed Registry there is none.
⍝ By default a simple character vector (⎕UCS 10-separated) is returned.
⍝ You may change this by specifying a 1 as `⍺`.
 nested←{0<⎕NC ⍵:⍎⍵ ⋄ 0}'nested'
 filename←path,'/',GetIndexFilename
 :If 0<##.F.IsFile filename
     r←⊃##.F.NGET filename nested
 :ElseIf 'Client'≡{⍵↑⍨{1+-⍵⍳'.'}⌽⍵}{⍵⊃⍨+/∧\∨/¨'Tatin.'∘⍷¨⍵}⎕NSI
     r←''
 :Else
     'Not found'⎕SIGNAL 98
 :EndIf
⍝Done
