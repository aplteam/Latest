 {r}←ExecuteMaintenanceCode G;list;codeFile;ref;body;function;pkgFolderList;path;qdmx
⍝ Maintenance code can be executed by putting an .aplf file into a folder `INI:Config:MaintenancePath'.\\
⍝ All those functions are executed once: after the first execution the text file is
⍝ renamed by adding an extension `.executed`.\\
⍝ Note that when this function finds both "foo.aplf" and "foo.aplf.executed" then it will not execute "foo.aplf".\\
⍝ The main purpose of this feature is to be able to polish anything in package config files, particularly tags.
 r←⍬
 :If 0<≢path←⊃G.INI.Get'Config:MaintenancePath'
 :AndIf ⎕NEXISTS path
 :AndIf 0<≢list←⊃F.Dir path,'/*.aplf'
     :For codeFile :In list
         :If ~⎕NEXISTS codeFile,'.executed' ⍝ In case a new version brings along a maintenance file that has already been executed
             ref←⎕NS''
             body←⊃F.NGET codeFile 1
             Reg.Assert' '=1↑0⍴ref.⎕FX body
             function←ref.⍎⊃↓ref.⎕NL 3
             pkgFolderList←,Reg.GetAllPackages G.RegistryPath
             :Trap 0
                 G.RegistryPath(function ##.Admin.LoopThrough)pkgFolderList
                 (codeFile,'.executed')(⎕NMOVE Reg.F.ExecNfunction)codeFile
             :Else
                 qdmx←⎕DMX
                 ('Culprit was the maintenance file ',⊃,/1↓⎕NPARTS codeFile)SendCrashMail ⍬
                 (codeFile,'.crashed')(⎕NMOVE Reg.F.ExecNfunction)codeFile
             :EndTrap
         :EndIf
     :EndFor
 :EndIf
