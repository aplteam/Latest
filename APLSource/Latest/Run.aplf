 r←Run(path match stats noOf);acreFlag;F;A;bool;projects;buff;pathFlag
⍝ Handles all three cases:
⍝ * Investigate the workspace (simple namespace or #)
⍝ * Investigate (currently tracked/opened) acre projects
⍝ * Investigate an acre project that is not currently tracked (open)
 A←##.APLTreeUtils ⋄ F←##.FilesAndDirs
 r←''
 pathFlag←acreFlag←0
 :If 0=≢path                            ⍝ True if we deal with an acre project
     :If 0=acreFlag←9=⎕SE.⎕NC'acre'
         'acre is required in ⎕SE but not available'⎕SIGNAL 6
     :EndIf
 :ElseIf ~(1⍴path)∊'#⎕'                 ⍝ True if it's a folder path
     :If 0=≢path
         path←'expand'F.NormalizePath F.PWD,F.CurrentSep
     :Else
         path←('expand'F.NormalizePath path),F.CurrentSep
         pathFlag←1
     :EndIf
 :EndIf
 :If acreFlag
 :AndIf 0≠≢projects←⎕SE.acre.Projects ⍬
     :If 1=≢projects
         path←(⊃projects[0;1]),'APLSource',F.CurrentSep
     :Else
         path←SelectProject projects
         →(0=≢path)/0
         path,←'APLSource',F.CurrentSep
     :EndIf
     path←'expand'F.NormalizePath path
 :Else
     acreFlag←0
 :EndIf
 :If acreFlag∨pathFlag
     :If F.IsDir path
         r←⍉⊃('recursive' 1)('type'(0 1 3))F.Dir path
         r←(r[;1]=2)⌿r                 ⍝ Only regular files
         r[;2]←↓A.FormatDateTime⊃r[;2]
         r←r[⍋⊃r[;2];0 2]
         :If stats
             buff←10↑¨r[;1]
             r←⊖(∪buff),[0.5]buff{+/⍵}⌸(≢r)⍴1
             r←r[⍋r[;0];]
         :Else
      ⍝ `noOf` is ...
      ⍝ ⍬ in case no argument was provided
      ⍝ 0 in case text was provided as argument
      ⍝ An integer otherwise.
             :If ⍬≡noOf
                 r←((10↑¨r[;1])≡¨⊂10↑1⊃r[¯1+≢r;])⌿r
             :ElseIf 0≢noOf
                 :If ¯1=×noOf
                     buff←10↑¨r[;1]  ⍝ YYYY-MM-DD
                     r←(buff∊noOf{(⍺⌊≢⍵)↑⍵}⌽∪⌽buff)⌿r
                 :Else
                     r←(noOf⌊≢r)↑r
                 :EndIf
             :EndIf
             :If 0=+/≢¨r[;0]
                 r←0 2⍴''
             :EndIf
             :If 0<≢r
                 :If pathFlag
                     r[;0]←(≢path)↓¨r[;0]
                 :Else
                     r[;0]←⎕SE.acre.Itemname r[;0]
                 :EndIf
                 :If 0<≢match
                     r←(match{⍺∘≡¨(≢⍺)↑¨⍵}r[;0])⌿r
                 :EndIf
             :EndIf
         :EndIf
     :Else
         r←0 0⍴''
     :EndIf
 :ElseIf ((,'#')≡,path)∨(('⎕SE')≡##.APLTreeUtils.Uppercase,path)∨9=⎕NC path
     :If 0=≢noOf
     :AndIf 0=stats
         noOf←¯1
     :EndIf
     :If (,'#')≡,path
         r←List noOf 1
     :Else
         r←path List noOf 1
     :EndIf
     :If stats
         buff←10↑¨r[;1]
         r←(∪buff),[0.5]buff{+/⍵}⌸(≢r)⍴1
     :ElseIf 0<≢match
         r←match{⍵⌿⍨⍺∘≡¨(≢⍺)↑¨{⍵↑⍨-(⌽⍵)⍳'.'}¨⍵[;0]}r
     :EndIf
 :Else
     'Not Found'⎕SIGNAL 6
 :EndIf
