 R←Test_API_140(stopFlag batchFlag);⎕TRAP;data;expected;path;result;buff
⍝ "Stats" for an acre project
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')

 :If 0=⎕SE.⎕NC'acre'
     :Trap 0
         {}⎕SE.UCMD'acre.Version'
     :Else
         R←T._NotApplicable ⋄ →0
     :EndTrap
 :EndIf

 R←T._Failed

 path←##.CiderConfig.HOME,'/TestProject'
 result←⎕SE.acre.OpenProject path'#.TEMP2'
 Assert'#.TEMP2'≡result

 data←##.Latest.Run path 1 1 0 ⍬
 →T.GoToTidyUp(,2)≢⍴⍴data
 buff←##.Latest.Run path 1 0 0 999
 →T.GoToTidyUp(≢buff)≠+/data[;2]

 R←T._OK

∆TidyUp:
 {}⎕SE.acre.CloseProject'#.TEMP2'
⍝Done
