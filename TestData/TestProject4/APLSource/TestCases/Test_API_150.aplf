 R←Test_API_150(stopFlag batchFlag);⎕TRAP;data;expected;path;result;path2
⍝ List for an acre project
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')

 :If 0=⎕SE.⎕NC'acre'
     :Trap 0
         {}⎕SE.UCMD'acre.Version'
     :Else
         R←T._NotApplicable ⋄ →0
     :EndTrap
 :EndIf

 R←T._Failed

 #.⎕SHADOW'TEMP2'

 path←##.CiderConfig.HOME,'/TestProject'
 result←⎕SE.acre.OpenProject path'#.TEMP2'
 Assert'#.TEMP2'≡result

 path2←path,'/APLSource/'
 data←##.Latest.Run path2 1 0 0 ⍬
 expected←{⍵/⍨FilesAndDirs.IsFile ⍵}1⊃('recursive' 1)FilesAndDirs.Dir path2
 expected←(≢path2)↓¨expected
 →T.GoToTidyUp expected≢data[;1]

 data←##.Latest.Run path2 1 0 0 3
 →T.GoToTidyUp 3≠≢data

 R←T._OK

∆TidyUp:
 {}⎕SE.acre.CloseProject'#.TEMP2'
⍝Done
