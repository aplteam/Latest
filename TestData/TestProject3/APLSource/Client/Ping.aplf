 bool←{timeout}Ping url_;client;request;url_;buff;qdmx;b;last
⍝ Establish whether the host is up and running (with very little overhead) in case it is an http[s] request
 bool←0
 timeout←{0<⎕NC ⍵:⍎⍵ ⋄ 4}'timeout'
 :If ∧/'[]'∊url_
     url_←ReplaceRegistryAlias url_
 :Else
     url_←url_
 :EndIf
 :If 0<≢url_
     :If 0=≢'^http[s]://'⎕S 0⊣⎕C url_
         bool←F.IsDir url_
     :Else
         request←R.NewRequest 0
         request.RequestTarget←'/ping'
         :If 0=⎕NC'∆PermanentConnections'
         :OrIf 9≠∆PermanentConnections.⎕NC 1 CompileNameFromUrl url_
             client←NewClient url_
             client.ClientTimeout←timeout
             :Trap 345
                 buff←client R.SendAndReceive request
                 bool←1
             :Else
                 ⍝ Just for debugging
             :EndTrap
         :Else
             client←∆PermanentConnections.⍎1 CompileNameFromUrl url_
             :Trap 345
                 {}client R.Send request                ⍝ Pipeline requests
                 buff←client R.Receive request          ⍝ Wait for all
                 bool←1
             :Else
                 ⍝ Just for debugging
             :EndTrap
         :EndIf
     :EndIf
 :EndIf
⍝Done
