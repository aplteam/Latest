 r←Test_CheckVersions_060(stopFlag batchFlag);⎕TRAP;dir;versions;result;oldPriorities;json;buff;rc;msg
⍝ Run CheckForLaterVersion on package from Registry not on Tatin Registry search path (priority←0)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 dir←∆GetTempDir''
 dir ⎕NCOPY⍠('Wildcard' 1)⊣'./TestData/myproj/*'

 json←##.Client.MyUserSettings.GetRegistry'https://test.tatin.dev/'
 oldPriorities←json.priority
 json.priority←0
 ##.Client.MyUserSettings.ReplaceRegistry json

 buff←1 TC.CheckForLaterVersion dir,'/packages'
 →T.GoToTidyUp~∨/'<<< Not found anywhere >>>'⍷⎕FMT buff

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce dir
 msg Assert rc=0
 json.priority←oldPriorities
 ##.Client.MyUserSettings.ReplaceRegistry json
⍝Done
