 r←Test_Load_081(stopFlag batchFlag);⎕TRAP;refs;html;ns;expected
⍝ Load a package that has several dependencies (local & remote) into non-existing ⎕SE.TEMP.Foo
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'⎕SE'
 ⎕SE.⎕SHADOW'TEMP' ⋄ 'TEMP'⎕SE.⎕NS''    ⍝ Used to load stuff into

 refs←TC.LoadPackages'./TestData/Tatin/Registry/aplteam-MarkAPL-11.0.3' '⎕SE.TEMP.Foo'
 →T.GoToTidyUp 0=≢refs
 (html ns)←⎕SE.TEMP.Foo.MarkAPL.Markdown2HTML'A Para' '' '* This' '* That'
 expected←'<p>A Para</p>' '<ul>' '<li>This</li>' '<li>That</li>' '</ul>'
 →T.GoToTidyUp expected≢html
 →T.GoToTidyUp~1<≢⎕SE._tatin.⎕NL 9
 →T.GoToTidyUp~0<≢⎕SE.TEMP.Foo.MarkAPL.##.TatinVars.ASSETS

 r←T._OK

∆TidyUp:
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
