 r←Test_BuildPackage_007(stopFlag batchFlag);⎕TRAP;dir;src;dist;cfg;zipfile;ns;list;expected;rc;msg;parms
⍝ Build a package with a code file with a file and a directory as "assets"
⍝ Requires "source" in Client Config to point to correct folder
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 r←T._Failed

 #.⎕EX TC.GetTatinRootSpace'#'
 dir←∆GetTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 F.MkDir(src,'/source')dist(src,'/docs')

 ('foo1←{1}')F.NPUT src,'/source/foo1.apln'
 cfg←TC.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Package_B'
 cfg.version←'0.1.0'
 cfg.source←'source'
 (F.ExpandPath src,'/LICENSE')⎕NCOPY F.ExpandPath'LICENSE'
 (F.ExpandPath src,'/docs/Managing_packages_with_Tatin.md')⎕NCOPY F.ExpandPath'docs_archive/Managing_packages_with_Tatin.md'
 cfg.assets←'docs/Managing_packages_with_Tatin.md'
 cfg.os_win←1
 TC.WritePackageConfigFile src cfg

 parms←TC.CreateBuildParms src
 parms.targetPath←dist
 zipfile←TC.BuildPackage parms
 →T.GoToTidyUp 0≥≢zipfile
 list←∆GetFileListFromZipArchive zipfile
 expected←'apl-package.json' 'source/foo1.apln' 'LICENSE' 'docs/Managing_packages_with_Tatin.md'
 →T.GoToTidyUp~expected ∆LazyMatchPaths list

 r←T._OK

∆TidyUp:
 (rc msg)←F.RmDirByForce dir
 msg Assert rc=0
 #.⎕EX TC.GetTatinRootSpace'#'
⍝Done
