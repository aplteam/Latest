 Initialize_Client
⍝ Copies dependencies, sets the working dir, initializes Conga and establishes references for the Tatin Client.\\
⍝ This function is intended to be called in development only.\\
⍝ It does not prepare for tests; for that call `#.Tatin.TestCases.Prepare`, which will call this function along the way.\\
 ##.FilesAndDirs.(APLTreeUtils2 OS)←##.(APLTreeUtils2 OS)
 :If 'CLEAR WS'≡⎕WSID
     :If 0<##.⎕NC'CiderConfig'                                 ⍝ Managed by ...
     :AndIf 2=##.CiderConfig.⎕NC'HOME'                         ⍝ ...LINK/Cider?!
         {}##.FilesAndDirs.Cd ##.CiderConfig.HOME
     :EndIf
 :Else
     ##.FilesAndDirs.PolishCurrentDir
 :EndIf
 CopyDependencies
 ##.Client.CreateAllRefs_Client
 ##.RumbaLean.⎕EX'DRC'                                         ⍝ Paranoia; with DRC around Conga won't be initialised
 ##.RumbaLean.⎕EX'Conga'
 'Conga'##.RumbaLean.⎕CY(2 ⎕NQ #'GetEnvironment' 'DYALOG'),'/ws/conga.dws'
 InitConga''
 ##.Client.InitUserSettings ##.Client.GetUserHomeFolder''
⍝Done
