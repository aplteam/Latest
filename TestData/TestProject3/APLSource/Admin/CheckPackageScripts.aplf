 CheckPackageScripts flag;pkgFolder;aplFolder;folders;folder;content;filename;path;extension;F;json;cfg;source;errorCounter
⍝ Tatin cannot load packages it uses by itself when bootstrapping.
⍝ Therefore we use a semi-automated approach: we install all packages in a sub-folder packages as usual.
⍝ But we don't (can't) load from that folder, so we copy all scripts into APLSource "manually"
⍝ This script compares the scripts with the packages in packages/ - they should be the same!
⍝ Note that packages that are not single scripts (like FilesAndDirs) are not handled that way.
 :If flag
     errorCounter←0
     F←##.FilesAndDirs
     pkgFolder←##.CiderConfig.HOME,'/packages/'
     aplFolder←##.CiderConfig.HOME,'/APLSource/'
     folders←F.ListDirs pkgFolder
     :For folder :In folders
         json←⊃F.NGET folder,'/apl-package.json'
         cfg←##.Registry.APLfromJSON json
         source←{0=≢⍵:⍵ ⋄ '/',1⊃⎕NPARTS ⍵}cfg.source
         :If F.IsDir folder,source
             content←F.ListFiles folder,source
             :If 1=≢content
                 (path filename extension)←⎕NPARTS 1⊃content
             :AndIf (⊂extension)∊'.aplc' '.apln'
             :AndIf F.IsFile aplFolder,filename,extension
             :AndIf ≢/⊃∘F.NGET¨(aplFolder,filename,extension)(1⊃content)
                 errorCounter+←1
                 ⎕←filename,' differs in ',aplFolder,' and ',folder
             :EndIf
         :Else
             ⎕←'Ignores conventions: ',folder
         :EndIf
     :EndFor
     :If 0<errorCounter
         ⎕←'*** Found ',(⍕errorCounter),' problem',(1<errorCounter)/'s'
     :Else
         ⎕←'*** All scripts are up to date'
     :EndIf
 :EndIf
⍝Done
