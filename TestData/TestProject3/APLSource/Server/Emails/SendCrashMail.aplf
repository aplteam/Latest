 (rc msg)←{add}SendCrashMail INI;parms;myConnection;mailParms;convert;domain;rc;msg;log;folder;list;filename;S
⍝ The idea is to have a GMail address dedicated for http://[test].tatin.dev
⍝ but we use this only to logon the Google's SMTP server: the address is not monitored.
⍝ The real consignees are listed as "To". That way we don't have to be particularly
⍝ concerned regarding the password.
⍝ You may add additional information to the standard crash email by passing `add`.
⍝ This must be a simple character vector.
 msg←'' ⋄ rc←0
 :If INI.Get'EMAIL:active'
     add←{0<⎕NC ⍵:⍎⍵ ⋄ ''}'add'
     '⍺ must be a simplem character vector'##.Reg.Assert(⎕DR add)∊80 160
     S←##.##.SMTP
     convert←{0=≢⍵:⍵ ⋄ ⊃{⍺,',',⍵}/⊆⍵}
     parms←⎕NEW S.ConnectionParameters
     parms.Server←⊃INI.Get'EMAIL:SMTP_Server'
     parms.Port←⊃INI.Get'EMAIL:Port'
     parms.Userid←⊃INI.Get'EMAIL:To'
     parms.Password←⊃INI.Get'EMAIL:Password'
     myConnection←⎕NEW S.Connection(,⊂parms)
     mailParms←⎕NEW S.MailParameters
     mailParms.From←'do-not-reply@tatin.dev'
     mailParms.To←⊃INI.Get'EMAIL:To'
     mailParms.CC←convert⊃INI.Get'EMAIL:CC'
     domain←⊃INI.Get'EMAIL:Domain'
     mailParms.Subject←domain,' has crashed'
     mailParms.Body←⊃INI.Get'EMAIL:Body'
     mailParms.Body←'<DOMAIN>'⎕R domain⊣mailParms.Body
     :If 0<≢add
         mailParms.Body,←⊆add
     :EndIf
     mailParms.Body,←⎕UCS 13 10
     folder←⊃INI.Get'TRAP:Folder'
     :If 0<≢list←##.F.ListFiles folder,'*.html'
         filename←⊃¯1↑list
         mailParms.AddAttachment filename'text/html'
     :EndIf
     (rc msg log)←myConnection.Send mailParms
     :If 0=rc
         msg←'Email was sent successfully'
     :Else
         msg←'Failed with rc=',(⍕rc),'; ',¯1↓(⊆msg),¨⎕UCS 13
     :EndIf
 :EndIf
⍝Done
