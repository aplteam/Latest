 r←ProcessLogFile data;date;packageIDs;Reg;date2
⍝ Takes the data saved in the log file and reduces them to the essentials, aggregate just by day
 r←0 4⍴⊂''
 Reg←##.##.Registry
 :If 0<≢data←(∨/¨' GET 200 '∘⍷¨data)/data
     data←(1=(↑data)+.='/')⌿data
     data←(2≤(19↓[2]↑data)+.='-')/data
 :AndIf 0<≢data←↑data
     date←10↑[2]data
     date2←(⊃∘##.A.ToNum¨↓data[;⍳4]),(⊃∘##.A.ToNum¨↓data[;5+⍳2]),[1.5](⊃∘##.A.ToNum¨↓data[;8+⍳2])
     packageIDs←↓(≢'YYYY-MM-DD hh:mm:ss GET 200 /')↓[2]data
     packageIDs←↑{ind←⍸'-'=⍵ ⋄ ((ind[1]-1)↑⍵)((ind[2]-1+ind[1])↑ind[1]↓⍵)(ind[2]↓⍵)}¨packageIDs
     r←packageIDs,date2
     r←r[⍋r[;3];]
     r←r[⍋r[;2];]
     r←r[⍋r[;1];]
     r←r[⍋r[;6];]
     r←r[⍋r[;5];]
     r←r[⍋r[;4];]
     r←{⍺,≢⍵}⌸r[;6~⍨⍳2⊃⍴r]  ⍝ Without the day
 :EndIf
 ⍝Done
