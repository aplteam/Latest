 {r}←{TS}SplitLogFile(oldYear folder);d;ind;logFilename;yearFilename;statFilename;zip;zipFilename;colTitles;monthFilename
 TS←{0<⎕NC ⍵:⍎⍵ ⋄ ⎕TS}'TS'
 r←⍬
 CreateZippedBackup folder
 logFilename←folder,GetLogFilename

 d←⊃⎕NGET logFilename 1                 ⍝ Data
 d←(0<≢¨d)/d                            ⍝ Get rid of empty lines, if any
 ind←+/∧\(↑d)[;⍳4]∧.=⍕oldYear
 :If ind≠≢d
     :Hold 'App-Log'
         yearFilename←folder,'year'
         monthFilename←folder,'month'
         (⊂ind↓d)⎕NPUT logFilename 1
         (⊂⍕1⊃TS)⎕NPUT yearFilename 1
         (⊂⍕2⊃TS)⎕NPUT monthFilename 1
     :EndHold
     :If 0<≢d←ind⍴d
     :AndIf 0<≢d←ProcessLogFile d
         statFilename←folder,'usage-data-',(⍕oldYear),'.csv'
         colTitles←'Group' 'Name' 'Version' 'Year' 'Month' 'Downloads'
         d colTitles ⎕CSV statFilename
         zipFilename←⊃,/(¯1↓⎕NPARTS statFilename),⊂'.zip'
         ##.F.DeleteFile zipFilename
         zip←⎕NEW ##.ZipArchive(,⊂zipFilename)
         folder zip.Add(≢folder)↓statFilename
     :EndIf
 :EndIf
⍝Done
