 html←packageIDs PrepareHtmlPageForPackageList mat;col1;col2;col3;col4;col5;colTitles;col6;col7;colDescription
 html←HtmlHeader GetDataTableStuff ⍬
 html,←⊂'<h2>List of packages</h2>'
 html,←AddSpecialMessage ⍬
 colTitles←'Package name' 'Description' 'Major<br>Versions' 'Project URL' 'OS' 'UC' 'Tags'
 colDescription←'' '' 'Number of major versions' '' 'Operating System' 'User Command' ''
 html,←⊂H.OpenTable+/(≢¨colTitles[2,2~⍨⍳2⊃⍴mat])⌈(≢⍕⌈/mat[;2]),⌈⌿≢¨mat[;2~⍨⍳2⊃⍴mat]
 html,←⊂'<thead>'
 html,←⊂'<tr class="my-first-th">'
 html,←colDescription{'<th ',((0<≢⍺)/'title="',⍺,'" '),'style="align: left">',⍵,'</th>'}¨colTitles
 html,←⊂'</thead>'
 html,←⊂'<tbody>'
 :If 0<≢mat
     col1←{'<td><code>',({'<a href="',Reg.VersionOf_REST,'/packages/major_versions/',⍵,'" title="List major versions">',⍵,'</a>'}⍵),'</code></td>'}¨mat[;1]
     col2←{'<td>',⍵,'</td>'}¨⍕¨mat[;3]
     col3←{'<td align="right">',⍵,'</td>'}¨⍕¨mat[;2]
     col4←{'<td>',⍵,'</td>'}¨mat[;1]{0=≢⍵:'- ' ⋄ '<a href="',⍵,'" title="Opens ',⍵,' in a new tab" target="_blank">',(MassageProjectURL ⍵),'</a>'}¨mat[;4]
     col5←{'<td>',⍵,'</td>'}¨mat[;5]
     col6←{'<td>',⍵,'</td>'}¨mat[;6]
     col7←{'<td>',⍵,'</td>'}¨mat[;7]
     html,←⊃,/{(⊂'<tr>'),⍵,(⊂'</tr>')}¨↓⍉↑col1 col2 col3 col4 col5 col6 col7
     html,←⊂'</tr>'
 :EndIf
 html,←⊂'</tbody>'
 html,←⊂'</table>'
 html,←⊆GetFooter
 html,←⊂'</body>'
 html,←⊂'</html>'
 ⍝Done
