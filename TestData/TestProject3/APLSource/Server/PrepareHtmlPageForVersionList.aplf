 html←PrepareHtmlPageForVersionList(packageID data);links;col1;col2;col3;col4;bool;colTitles
 data←⊖data
 html←HtmlHeader ''
 html,←⊂'<h2>All versions of "',packageID,'"</h2>'
 html,←AddSpecialMessage ⍬
 colTitles←'Package ID' 'Publishing date' 'Config' 'Dependencies'
 html,←⊂H.OpenTable colTitles CalcTableWidth data
 html,←⊂'<thead>'
 html,←⊂'<tr class="my-first-th">'
 html,←{'<th>',⍵,'</th>'}¨colTitles
 html,←⊂'</thead>'
 html,←⊂'<tbody>'
 col1←{'<td><code>',⍵,'</code></td>'}¨data[;1]         ⍝ The package ID
 col2←{'<td>',(⍕⍵),'</td>'}¨data[;2]                   ⍝ The publishing Date
 links←{'<a href="',Reg.VersionOf_REST,'/packages/details/',⍵,'" title="Link to the package configuration">Show</a>'}¨data[;1]
 col3←{'<td>',⍵,'</td>'}¨links
 bool←PackageHasDependencies¨data[;1]
 links←bool{⍺:'<a href="',Reg.VersionOf_REST,'/packages/dependencies/',⍵,'" title="Link to the dependency definition">Show</a>' ⋄ 'None'}¨data[;1]
 col4←{'<td>',⍵,'</td>'}¨links
 html,←{'<tr>',⍵,'</tr>'}¨col1,¨col2,¨col3,¨col4
 html,←⊂'</tr>'
 html,←⊂'</tbody>'
 html,←⊂'</table>'
 html,←⊆GetFooter
 html,←⊂'</body>'
 html,←⊂'</html>'
⍝Done
